dice_expr = ${ expr ~ (WHITESPACE+ ~ expr)* }

expr      = _{ add_sub }
add_sub   = { mul_div ~ (WHITESPACE* ~ (add | subtract) ~ WHITESPACE* ~ mul_div)* }
mul_div   = { factor ~ (WHITESPACE* ~ (multiply | divide) ~ WHITESPACE* ~ factor)* }
factor    = _{ repetition | dice | number | parens }

repetition = { dice_term ~ parens ~ dice_modifier* }

dice = { dice_term? ~ roll ~ number ~ dice_modifier* }

dice_term = _{ number | parens }

dice_modifier = { (explode | keep_high | keep_low | drop_low | drop_high) ~ dice_term? }

keep_low  = { "kl" }
keep_high = { "kh" | "k" }
drop_low  = { "dl" }
drop_high = { "dh" | "d" }
explode   = { "!" }

roll = _{ "d" }

number = @{ ASCII_DIGIT+ }

add       = { "+" }
subtract  = { "-" }
multiply  = { "*" }
divide    = { "/" }

parens = _{ "(" ~ WHITESPACE* ~ expr ~ WHITESPACE* ~ ")" | "[" ~ WHITESPACE* ~ expr ~ WHITESPACE* ~ "]" }

WHITESPACE = _{ " " | "\t" }
